apply plugin: 'com.android.application'

ext {
    def getGitVersion = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        return (stdout.toString().replace("\n", "-0")).split('[-.]').take(3)
    }

    def getVersionCode = { ->
        def vers = getGitVersion()
        return vers[0].toInteger()*1000000 + vers[1].toInteger() * 1000 + vers[2].toInteger()
    }

    def getVersionName = { ->
        return getGitVersion().join('.')
    }

    def getName = { ->
        return (app_name+"_"+myVName)
    }

    myVName = getVersionName()
    myVCode = getVersionCode()
    myName = getName()
}

android {
    signingConfigs {
        release {
            keyAlias KEY_ALIAS
            keyPassword KEY_PASS
            storeFile file(STORAGE_FILE_PATH)
            storePassword STORAGE_PASS
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.lenovo.softy"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode myVCode
        versionName myVName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            setProperty("archivesBaseName", myName)
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            android.applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    outputFileName =
                            outputFileName.replace("-debug", "")
                    outputFileName =
                            outputFileName.replace("-release", "")
                    outputFileName =
                            outputFileName.replace("-full", "")
                }
            }
        }
    }
    flavorDimensions "ver"
    productFlavors {
        full {
            dimension "ver"
            minSdkVersion 23
            applicationId 'com.example.lenovo.softy.full'
            targetSdkVersion 28
            testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
            manifestPlaceholders = [orientation:"portrait"]
        }
        dev {
            dimension "ver"
            versionNameSuffix "-dev"
            minSdkVersion 23
            applicationId 'com.example.lenovo.softy.dev'
            targetSdkVersion 28
            testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
            manifestPlaceholders = [orientation:"sensor"]
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

//task('increaseVersionCode') << {
//    def counter = Integer.parseInt(release_count) + 1
//    ant.propertyfile(file: "../gradle.properties") {
//        entry(key: "release_count", value: counter)
//    }
//}
//
//tasks.whenTaskAdded { task ->
//    if (task.name == 'generateFullReleaseBuildConfig') {
//        task.dependsOn 'increaseVersionCode'
//    }
//}